apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.cdvdev.basetemplate"
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        //generate apk with custom name
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = variant.productFlavors[0].applicationId + "-v" + defaultConfig.versionName + "-" + variant.buildType.name + ".apk";
                    output.outputFile = new File(outputFile.parent, fileName);
                }
            }
        }

    }

    productFlavors {
        production {
            applicationId defaultConfig.applicationId
            versionName defaultConfig.versionName
        }

        demo {
            applicationId defaultConfig.applicationId + ".demo"
            versionName defaultConfig.versionName + "-demo"
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res/layouts/activity',
                    'src/main/res/layouts/fragment',
                    'src/main/res/layouts/adapter',
                    'src/main/res/layouts/appbar',
                    'src/main/res/other'
                    , 'src/main/res/layouts/other']
        }

        demo {
            res.srcDirs = [
                    'src/demo/res',
                    'src/demo/res/layouts/activity',
                    'src/demo/res/layouts/fragment',
                    'src/demo/res/layouts/adapter',
                    'src/demo/res/layouts/appbar',
                    'src/demo/res/other'
            ]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}
